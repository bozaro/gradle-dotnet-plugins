defaultTasks 'test'

version = '0.1.0.1'

allprojects {
	apply plugin: 'idea'
	apply plugin: 'base'

	group = 'gradle-dotnet-plugins'

	uploadArchives {
		repositories {
			ivy { url rootProject.file('../repository') }
		}
	}

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'groovy'

	dependencies {
		compile gradleApi()
		compile localGroovy()
		def spockframework = 'org.spockframework:spock-core:0.7-groovy-1.8'
		if (project.name == 'testing') {
			compile spockframework
		} else {
			testCompile project(':testing')
		}
		testCompile spockframework
		testRuntime "cglib:cglib-nodep:2.2"     // enables mocking of classes (in addition to interfaces)
	}

	task publish(dependsOn: [test, uploadArchives])
}

dependencies {
	'default' subprojects.findAll { it.name != 'testing' }.collect { project(it.path) }
}

task publish(dependsOn: uploadArchives)
